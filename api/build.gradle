import java.time.Instant

plugins {
    id 'application'
    id 'com.github.ben-manes.versions' version "$dependencyUpdatesPluginVersion"
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version "$spotbugsPluginVersion"
}

group = 'com.ruchij'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "io.javalin.community.openapi:openapi-annotation-processor:$openApiVersion"

    implementation(
        "io.javalin:javalin-bundle:$javalinVersion",
        "com.typesafe:config:$typesafeConfigVersion",
        "ch.qos.logback:logback-classic:$logbackVersion",
        "net.logstash.logback:logstash-logback-encoder:$logstashLogbackVersion",
        'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
        "io.javalin.community.openapi:javalin-openapi-plugin:$openApiVersion",
        "io.javalin.community.openapi:javalin-swagger-plugin:$openApiVersion"
    )

    testImplementation(
        platform("org.junit:junit-bom:$junitVersion"),
        'org.junit.jupiter:junit-jupiter',
        'org.junit.platform:junit-platform-launcher',
        "org.mockito:mockito-core:$mockitoVersion"
    )
}

application {
    mainClass = 'com.ruchij.App'
    applicationDefaultJvmArgs = [
        '-Dlogback.configurationFile=/opt/data/logback.xml'
    ]
}

def buildDirectory = layout.buildDirectory.dir("generated/src/main/java").get()

tasks.register("generateBuildInfo") {
    doLast {
        def buildInfoDirectory = buildDirectory.dir("com/ruchij/build")
        buildInfoDirectory.asFile.mkdirs()

        def buildInfoFile = buildInfoDirectory.file("BuildInfo.java").asFile

        buildInfoFile.text = """
            package com.ruchij.build;

            import java.time.Instant;
            
            public final class BuildInfo {
                public static final String APPLICATION_NAME = "javalin-seed";
                public static final String APPLICATION_GROUP = "${project.group}";
                public static final String GRADLE_VERSION = "${gradle.gradleVersion}";
                public static final Instant BUILD_TIMESTAMP = Instant.parse("${Instant.now()}");
                public static final String GIT_BRANCH = "${'git rev-parse --abbrev-ref HEAD'.execute().text.trim()}";
                public static final String GIT_COMMIT = "${'git rev-parse --short HEAD'.execute().text.trim()}";
            }
        """.stripIndent()
    }
}

compileJava {
    dependsOn generateBuildInfo
}

sourceSets {
    main {
        java {
            srcDirs += buildDirectory
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

distTar {
    archiveFileName = 'api.tar'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion = checkstyleVersion
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    excludeFilter = rootProject.file('config/spotbugs/exclude.xml')
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        html.required = true
        xml.required = true
    }
}