import java.time.Instant

plugins {
    id 'application'
    id 'com.github.ben-manes.versions' version "$dependencyUpdatesPluginVersion"
    id 'net.researchgate.release' version "$releasePluginVersion"
    id 'jacoco'
}

group = 'com.ruchij'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
        "io.javalin:javalin-bundle:$javalinVersion",
        "com.typesafe:config:$typesafeConfigVersion",
        "ch.qos.logback:logback-classic:$logbackVersion",
        "net.logstash.logback:logstash-logback-encoder:$logstashLogbackVersion",
        'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    )

    testImplementation(
        platform("org.junit:junit-bom:$junitVersion"),
        'org.junit.jupiter:junit-jupiter',
        'org.junit.platform:junit-platform-launcher',
        "org.mockito:mockito-core:$mockitoVersion"
    )
}

application {
    mainClass = 'com.ruchij.App'
    applicationDefaultJvmArgs = [
        '-Dlogback.configurationFile=/opt/data/logback.xml'
    ]
}

def buildDirectory = layout.buildDirectory.dir("generated/src/main/java").get()

tasks.register("generateBuildInfo") {
    doLast {
        def buildInfoDirectory = buildDirectory.dir("com/ruchij/build")
        buildInfoDirectory.asFile.mkdirs()

        def buildInfoFile = buildInfoDirectory.file("BuildInfo.java").asFile

        buildInfoFile.text = """
            package com.ruchij.build;

            import java.time.Instant;
            
            public final class BuildInfo {
                public static final String APPLICATION_NAME = "javalin-seed";
                public static final String APPLICATION_GROUP = "${project.group}";
                public static final String APPLICATION_VERSION = "${project.version}";
                public static final String GRADLE_VERSION = "${gradle.gradleVersion}";
                public static final Instant BUILD_TIMESTAMP = Instant.parse("${Instant.now()}");
                public static final String GIT_BRANCH = "${'git rev-parse --abbrev-ref HEAD'.execute().text.trim()}";
                public static final String GIT_COMMIT = "${'git rev-parse --short HEAD'.execute().text.trim()}";
            }
        """.stripIndent()
    }
}

compileJava {
    dependsOn generateBuildInfo
}

sourceSets {
    main {
        java {
            srcDirs += buildDirectory
        }
    }
}

release {
    preTagCommitMessage = 'Setting release version: '
    newVersionCommitMessage = 'Setting development version: '
    pushReleaseVersionBranch = 'main'

    git {
        requireBranch.set('dev')
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

distTar {
    archiveFileName = 'api.tar'
}

jacocoTestReport {
    dependsOn test
}
